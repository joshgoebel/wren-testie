import "random" for Random
import "../vendor/colors" for Colors as Color
import "../src/stacktrace_report" for StackTraceReport
import "../src/capabilities" for Capabilities

var RND = Random.new()
var SAD_EMOJI = ["ðŸ˜¡","ðŸ‘º","ðŸ‘¿","ðŸ™€","ðŸ’©","ðŸ˜°","ðŸ˜¤","ðŸ˜¬"]

class CuteReporter {
    construct new(name) {
        _name = name
        _fail = _skip = _success = 0
        _tests = []
        _fails = []
        _section = name.trim()
    }
    start() { System.print(_name) }
    skip(name) {
        _skip = _skip + 1
        System.print("  ðŸŒ€ [skip] %(name)")
    }
    section(name) {
        _section = name.trim()
        System.print("\n  %(name)\n")
    }
    fail(name, fiber) {
        _fail = _fail + 1
        // System.print("  âŒ %(name) \n     %(error)\n")
        System.print("  âŒ %(name)")
        _fails.add([_section,name,fiber])
    }
    success(name) {
        _success = _success + 1
        System.print("  âœ… %(name)")
    }
    sadEmotion { SAD_EMOJI[RND.int(SAD_EMOJI.count)] }
    printErrors() {
        System.print()
        for (fail in _fails) {
            var section = fail[0]
            var name = fail[1]
            var fiber = fail[2]
            var error = fiber.error
            System.print(Color.RED + Color.BOLD + "â— %(section) -> %(name)" + Color.RESET + "\n")
            if (error is String) {
                System.print(error)
            } else {
                System.print(error.error)
            }
            System.print()
            if (Capabilities.hasMirror) {
                var s = StackTraceReport.new(fiber)
                s.print()
            }
        }
    }
    done() {
        printErrors()
        var overall = "ðŸ’¯"
        if (_fail > 0) overall = "%(sadEmotion)"
        _groups = []
        if (_fail > 0) _groups.add("%(Color.RED + Color.BOLD)âœ• %(_fail) failed%(Color.RESET)")
        if (_skip > 0) _groups.add("%(Color.YELLOW + Color.BOLD)â˜ %(_skip) skipped%(Color.RESET)")
        if (_success>0) {
            _groups.add("%(Color.GREEN)âœ“ %(_success) passed%(Color.RESET)")
        } else {
            _groups.add("âœ“ %(_success) passed")
        }
        var total = _fail + _skip + _success
        _groups.add("%(total) total")

        System.print("Tests:  %(overall) %(_groups.join(", "))\n")
    }
}
